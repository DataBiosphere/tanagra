plugins {
    id "java"
    id "maven-publish"

    // See /buildSrc/build.gradle
    id 'tanagra.java-conventions'

    id 'com.google.cloud.tools.jib' version '3.1.2'
    id "com.jfrog.artifactory" version "4.18.2"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "org.hidetake.swagger.generator" version "2.18.2"
    id "org.springframework.boot" version "2.5.1"
    id "org.barfuin.gradle.taskinfo" version "1.1.1"
}

sourceCompatibility = JavaVersion.VERSION_11

group = "bio.terra"
version = "0.0.0-SNAPSHOT"

ext {
    artifactGroup = "${group}.tanagra"
    openapiOutputDir = "${buildDir}/openapi"
    resourceDir = "${projectDir}/src/main/resources"
}

repositories {
    mavenCentral()
    maven {
        url "https://broadinstitute.jfrog.io/broadinstitute/libs-release/"
    }
    maven {
        url "https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/"
    }
    maven {
        url "https://packages.confluent.io/maven/"
    }
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
    dependencies {
        dependency group: "io.swagger.core.v3", name: "swagger-annotations", version: "2.1.5"
        dependency group: "io.swagger.codegen.v3", name: "swagger-codegen-cli", version: "3.0.22"
    }
}

dependencies {
    ext {
        beam = "2.32.0"
        jackson = "2.12.3"
        jacksonYaml = "2.11.2"
    }

    implementation group: "bio.terra", name: "terra-common-lib", version: "0.0.69-SNAPSHOT"
    implementation group: "org.apache.commons", name: "commons-text", version: "1.9"
    annotationProcessor group: "org.springframework.boot", name: "spring-boot-configuration-processor"

    // GCP BOM version controlled libraries. GCP BOM specified in buildSrc/
    implementation group: "com.google.cloud", name: "google-cloud-bigquery"
    implementation group: "com.google.guava", name: "guava"

    // Dependencies whose versions are controlled by Spring
    implementation group: "javax.validation", name: "validation-api"
    implementation group: "org.apache.commons", name: "commons-dbcp2"
    implementation group: "org.apache.commons", name: "commons-pool2"
    implementation group: "org.springframework.boot", name: "spring-boot-starter-web"
    implementation group: "org.springframework.boot", name: "spring-boot-starter-data-jdbc"

    // OpenApi(Swagger) dependencies
    implementation group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-yaml", version: jacksonYaml
    implementation group: "com.fasterxml.jackson.module", name: "jackson-module-jaxb-annotations", version: jacksonYaml
    implementation "com.fasterxml.jackson.core:jackson-core:${jackson}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson}"
    implementation group: "io.swagger.core.v3", name: "swagger-annotations"
    implementation group: "io.swagger", name: "swagger-annotations", version: "1.6.2"
    implementation group: "org.webjars", name: "webjars-locator-core", version: "0.46"
    runtimeOnly group: "org.webjars.npm", name: "swagger-ui-dist", version: "3.36.2"
    swaggerCodegen group: "io.swagger.codegen.v3", name: "swagger-codegen-cli"

    // Beam libraries
    implementation "org.apache.beam:beam-sdks-java-core:${beam}"
    implementation "org.apache.beam:beam-sdks-java-io-google-cloud-platform:${beam}"
    implementation "org.apache.beam:beam-sdks-java-io-jdbc:${beam}"
    implementation "org.apache.beam:beam-sdks-java-extensions-google-cloud-platform-core:${beam}"
    testRuntimeClasspath "org.apache.beam:beam-runners-direct-java:${beam}"
    implementation "org.apache.beam:beam-runners-google-cloud-dataflow-java:${beam}"

    // Test dependencies
    // Jupiter version is managed by the Spring Boot plugin
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter"
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api"
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }

    // Static analysis
    compileOnly "com.google.code.findbugs:annotations:3.0.1u2"
    testCompileOnly "com.google.code.findbugs:annotations:3.0.1u2"
}

test {
    useJUnitPlatform()

    // These System properties are used for the generated SQL strings to compare against during tests.
    // See bio.terra.tanagra.aousynthetic.GeneratedSqlUtils for how they are used.
    systemProperty("GRADLE_PROJECT_DIR", projectDir)
    if (project.findProperty("generateSqlFiles")) {
        systemProperty("GENERATE_SQL_FILES", "true")
    }
}

// Apply Gradle Script Plugins for more modular gradle files.
// See also https://docs.gradle.org/current/userguide/plugins.html#sec:script_plugins
apply from: "$rootDir/api/gradle/artifactory.gradle"
apply from: "$rootDir/api/gradle/jib.gradle"
apply from: "$rootDir/api/gradle/open-api.gradle"

// e.g. ./gradlew api:index -Dexec.args="input_underlay.json output_dir"
task index (type:JavaExec) {
    main = "bio.terra.tanagra.indexing.Main"
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
    args System.getProperty("exec.args", "").split()
}