import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'
    id 'maven-publish'

    id 'com.jfrog.artifactory' version '5.2.2'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.hidetake.swagger.generator' version '2.19.2'
    id 'org.springframework.boot' version '3.3.1'
}

repositories {
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
    google()
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

// Javadoc
java {
    // Builds Javadoc into the published package as part of the 'assemble' task.
    withJavadocJar()
    // Builds sources into the published package as part of the 'assemble' task.
    withSourcesJar()
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
    dependencies {
        dependency "io.swagger.core.v3:swagger-annotations:${gradle.vSwaggerAnnotations}"
        dependency "io.swagger.codegen.v3:swagger-codegen-cli:${gradle.vSwaggerCodegen}"
    }
}

dependencies {
    implementation "io.swagger.core.v3:swagger-annotations:${gradle.vSwaggerAnnotations}"
    swaggerCodegen "io.swagger.codegen.v3:swagger-codegen-cli:${gradle.vSwaggerCodegen}"

    implementation "com.fasterxml.jackson.core:jackson-core:${gradle.vJackson}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${gradle.vJackson}"

    implementation "org.glassfish.jersey.core:jersey-client:${gradle.vJersey3}"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:${gradle.vJersey3}"
    implementation "org.glassfish.jersey.media:jersey-media-multipart:${gradle.vJersey3}"
    implementation "org.glassfish.jersey.connectors:jersey-jdk-connector:${gradle.vJersey3}"
}

bootJar {
    enabled = false
}

String includeDir = "$projectDir/gradle"
apply(from: "${includeDir}/artifactory.gradle")
apply(from: "${includeDir}/open-api.gradle")
