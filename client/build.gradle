plugins {
    id 'java-library'
    id 'maven-publish'

    id 'com.jfrog.artifactory' version '5.2.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.hidetake.swagger.generator' version '2.18.2'
    id 'org.springframework.boot' version '2.5.1'
}

repositories {
    mavenCentral()
    maven {
        url "https://broadinstitute.jfrog.io/broadinstitute/libs-release/"
    }
    maven {
        url "https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/"
    }
    google()
}

group = "bio.terra"
version = gradle.tanagraVersion

//// Keep the client library at Java 11 for now
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

// Javadoc
java {
    // Builds Javadoc into the published package as part of the 'assemble' task.
    withJavadocJar()
    // Builds sources into the published package as part of the 'assemble' task.
    withSourcesJar()
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
    dependencies {
        dependency group: "io.swagger.core.v3", name: "swagger-annotations", version: "2.1.5"
        dependency group: "io.swagger.codegen.v3", name: "swagger-codegen-cli", version: "3.0.22"
    }
}

dependencies {
    ext {
        jerseyVersion = "2.32"
        jacksonDatatypeVersion = "2.11.3"
    }

    // Swagger deps, versions controlled by dependencyManagement in root project
    implementation group: "com.fasterxml.jackson.datatype", name: "jackson-datatype-jsr310", version: jacksonDatatypeVersion
    implementation group: "org.glassfish.jersey.core", name: "jersey-client", version: jerseyVersion
    implementation group: "org.glassfish.jersey.media", name: "jersey-media-json-jackson", version: jerseyVersion
    implementation group: "org.glassfish.jersey.media", name: "jersey-media-multipart", version: jerseyVersion
    implementation group: "org.glassfish.jersey.connectors", name: "jersey-jdk-connector", version:jerseyVersion
    implementation group: "io.swagger.core.v3", name: "swagger-annotations"
    implementation group: "io.swagger", name: "swagger-annotations", version: "1.6.2"
    swaggerCodegen group: "io.swagger.codegen.v3", name: "swagger-codegen-cli"
}

bootJar {
    enabled = false
}

def includeDir = "$projectDir/gradle"
apply(from: "$includeDir/artifactory.gradle")
apply(from: "$includeDir/open-api.gradle")
