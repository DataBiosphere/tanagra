// OpenAPI/Swagger Client Generation
String artifactGroup = "${gradle.tanagraArtifactGroup}"
String version = "${gradle.tanagraVersion}"
String openapiOutputDir = "${buildDir}/openapi"

swaggerSources {
    client {
        inputFile = file("${rootDir}/service/src/main/resources/api/service_openapi.yaml")
        code {
            jvmArgs = [
                    '--add-opens=java.base/java.util=ALL-UNNAMED'] // for Java 16+
            language = 'java'
            library = 'jersey2'
            outputDir = file(openapiOutputDir)
            rawOptions = [
                    '--model-package',
                    "${artifactGroup}.model",
                    '--api-package',
                    "${artifactGroup}.api",
                    '--invoker-package',
                    "${artifactGroup}.client",
                    '--group-id',
                    "${artifactGroup}",
                    '--artifact-version',
                    "${version}",
                    '--ignore-file-override',
                    "${projectDir}/.swagger-codegen-ignore",
                    '-D',
                    'apiTests=false,' +
                            'apiDocs=false,' +
                            'modelTests=false,' +
                            'modelDocs=false,' +
                            'dateLibrary=java8,' +
                            'jakarta=true'
            ]
        }
    }
}

// Internal dependencies
sourceSets.main.java.srcDir "${openapiOutputDir}/src/main/java"

tasks.compileJava.dependsOn(swaggerSources.client.code)
tasks.sourcesJar.dependsOn(generateSwaggerCodeClient)
