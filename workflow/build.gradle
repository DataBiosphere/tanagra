plugins {
    id "java"
    // See /buildSrc/build.gradle
    id 'tanagra.java-conventions'
}
repositories {
    mavenCentral()
    maven {
        url "https://packages.confluent.io/maven/"
    }
}
dependencies {
    ext {
        beam = "2.32.0"
    }
    implementation project(":lib")

    // Beam libraries
    implementation "org.apache.beam:beam-sdks-java-core:${beam}"
    implementation "org.apache.beam:beam-sdks-java-io-google-cloud-platform:${beam}"
    implementation "org.apache.beam:beam-sdks-java-io-jdbc:${beam}"
    implementation "org.apache.beam:beam-sdks-java-extensions-google-cloud-platform-core:${beam}"
    testRuntimeClasspath "org.apache.beam:beam-runners-direct-java:${beam}"
    implementation "org.apache.beam:beam-runners-google-cloud-dataflow-java:${beam}"

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.4"
    implementation "com.google.cloud.sql:postgres-socket-factory:1.3.3"
    implementation "org.apache.commons:commons-text:1.9"
    implementation "org.postgresql:postgresql:42.2.24"

    // GCP BOM version controlled libraries, see /buildSrc
    implementation "com.google.guava:guava"

    // ApacheBeam TestPipeline requires JUnit4. https://issues.apache.org/jira/browse/BEAM-3415
    testImplementation "junit:junit:4.12"
    testImplementation("org.hamcrest:hamcrest:2.2")
}
test {
    useJUnit()
}

// Sample task to allow executing workflow from gradle.
/*
 $ ./gradlew workflow:execute -DmainClass=bio.terra.tanagra.workflow.HelloWorld \
 -Dexec.args="--myArg0=foo --myArg1=bar" -Pdirect-runner
 */
task execute (type:JavaExec) {
    main = System.getProperty("mainClass")
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
    args System.getProperty("exec.args", "").split()
}
