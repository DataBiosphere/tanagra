plugins {
    id 'application'
    id 'java'
    id 'java-test-fixtures'
    id 'pmd'
    id 'tanagra.java-conventions'
}

sourceCompatibility = JavaVersion.VERSION_17

ext {
    resourceDir = "${projectDir}/src/main/resources"
}

repositories {
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
    maven {
        url 'https://packages.confluent.io/maven/'
    }
}

dependencies {
    implementation project(':underlay')
    testImplementation(testFixtures(project(':underlay')))

    implementation "org.apache.commons:commons-text:${gradle.vApacheCommonsText}"
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'

    // Command parsing
    implementation project(':cli')
    compileOnly "info.picocli:picocli:${gradle.vPicocliVersion}"
    annotationProcessor "info.picocli:picocli-codegen:${gradle.vPicocliVersion}"

    // GCP libraries versions are controlled by the BOM specified in buildSrc.
    implementation 'com.google.cloud:google-cloud-bigquery'
    implementation 'com.google.guava:guava'

    // Apache Beam libraries
    String vApacheBeam = '2.56.0'
    implementation "org.apache.beam:beam-sdks-java-core:${vApacheBeam}"
    implementation "org.apache.beam:beam-sdks-java-io-google-cloud-platform:${vApacheBeam}"
    implementation "org.apache.beam:beam-sdks-java-io-jdbc:${vApacheBeam}"
    implementation "org.apache.beam:beam-sdks-java-extensions-google-cloud-platform-core:${vApacheBeam}"
    implementation "org.apache.beam:beam-sdks-java-extensions-join-library:${vApacheBeam}"
    implementation "org.apache.beam:beam-runners-google-cloud-dataflow-java:${vApacheBeam}"
    implementation "org.apache.beam:beam-runners-direct-java:${vApacheBeam}"
    testImplementation "org.apache.beam:beam-runners-direct-java:${vApacheBeam}"

    // Apache Beam depends on specific JUnit versions
    // https://github.com/apache/beam/blob/release-2.56.0/buildSrc/src/main/groovy/org/apache/beam/gradle/BeamModulePlugin.groovy
    testImplementation 'junit:junit:4.13.1'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.7.0'

    // Static analysis
    pmd "net.sourceforge.pmd:pmd-ant:${gradle.vPmd}"
    compileOnly "com.github.spotbugs:spotbugs-annotations:${gradle.vSpotBugs}"
    testCompileOnly "com.github.spotbugs:spotbugs-annotations:${gradle.vSpotBugs}"
}

tasks.withType(Test).configureEach {
    // These System properties are used for the generated SQL strings to compare against during tests.
    // See bio.terra.tanagra.testing.GeneratedSqlUtils for how they are used.
    systemProperty('GRADLE_PROJECT_DIR', projectDir)
    if (project.findProperty('generateSqlFiles')) {
        systemProperty('GENERATE_SQL_FILES', 'true')
    }
}

application {
    mainClass = 'bio.terra.tanagra.indexing.cli.Main'
    applicationName = 'tanagra'
    executableDir = 'bin'

    // TODO: Suppress Jersey PATCH related warnings.
    applicationDefaultJvmArgs = [
        '--add-opens',
        'java.base/sun.net.www.protocol.https=ALL-UNNAMED',
        '--add-opens',
        'java.base/java.net=ALL-UNNAMED',
        // Compress Instance OOPs (manually set when max heap is over 32G
        // See - https://www.baeldung.com/jvm-compressed-oops
        // https://docs.github.com/en/actions/using-github-hosted-runners/about-github-hosted-runners#supported-runners-and-hardware-resources
        "-Xmn7g",
        "-Xmx14g",
        '-XX:+HeapDumpOnOutOfMemoryError'
    ]
}

tasks.register('generateManpageAsciiDoc', JavaExec) {
    dependsOn(classes)
    group = 'documentation'
    description = 'Generate AsciiDoc manpage'
    classpath(configurations.compileClasspath, configurations.annotationProcessor, sourceSets.main.runtimeClasspath)
    main 'picocli.codegen.docgen.manpage.ManPageGenerator'
    args 'bio.terra.tanagra.indexing.cli.Main', '--outdir=$projectDir/../docs/generated/indexer-cli', '--verbose', '--force'
}

apply from: "${rootDir}/gradle/spotless-dependencies.gradle"
apply from: "${rootDir}/gradle/version-properties.gradle"
